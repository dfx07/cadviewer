#version 330 core

in vec4  v_v4Color;
flat in vec2 vf_v2Pos1Px;
flat in vec2 vf_v2Pos2Px;
flat in vec2 vf_v2Pos3Px;

out vec4 FragColor;

// https://iquilezles.org/articles/distfunctions2d/
float sdTriangle( in vec2 p, in vec2 p0, in vec2 p1, in vec2 p2 )
{
    vec2 e0 = p1 - p0, e1 = p2 - p1, e2 = p0 - p2;
    vec2 v0 = p - p0, v1 = p - p1, v2 = p - p2;
    vec2 pq0 = v0 - e0 * clamp(dot(v0, e0) / dot(e0, e0), 0.0, 1.0);
    vec2 pq1 = v1 - e1 * clamp(dot(v1, e1) / dot(e1, e1), 0.0, 1.0);
    vec2 pq2 = v2 - e2 * clamp(dot(v2, e2) / dot(e2, e2), 0.0, 1.0);
    float s = sign(e0.x * e2.y - e0.y * e2.x);
    vec2 d = min(min(vec2(dot(pq0, pq0), s * (v0.x * e0.y - v0.y * e0.x)),
                     vec2(dot(pq1, pq1), s * (v1.x * e1.y - v1.y * e1.x))),
                vec2(dot(pq2, pq2), s * (v2.x * e2.y - v2.y * e2.x)));
    return -sqrt(d.x) * sign(d.y);
}

void main()
{
    vec2 v2PosPx = gl_FragCoord.xy;

    float fDist = sdTriangle(v2PosPx, vf_v2Pos1Px, vf_v2Pos2Px, vf_v2Pos3Px);

    float aa = fwidth(fDist) * 0.5;

    float fAlpha = 1.0 - smoothstep(-aa, aa, fDist);

    FragColor = vec4(v_v4Color.rgb, v_v4Color.a * fAlpha);
}